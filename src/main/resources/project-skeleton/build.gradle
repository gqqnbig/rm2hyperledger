/*
 * SPDX-License-Identifier: Apache-2.0
 */

plugins {
    id 'com.github.johnrengelman.shadow' version '5.1.0'
    id 'application'
//    id 'checkstyle'
    id 'jacoco'
}

wrapper { gradleVersion = '6.5' }

group 'org.hyperledger.fabric.samples'
version '1.0-SNAPSHOT'

dependencies {

    implementation 'org.hyperledger.fabric-chaincode-java:fabric-chaincode-shim:2.+'
    //fabric-chaincode-shim requires org.json.
    //org.json is serialization-only library, while genson supports deserialization.
    implementation 'com.owlike:genson:1.5'
    testImplementation 'org.hyperledger.fabric-chaincode-java:fabric-chaincode-shim:2.+'
    testImplementation 'org.junit.jupiter:junit-jupiter:5.4.2'
    testImplementation 'org.assertj:assertj-core:3.11.1'
    testImplementation 'org.mockito:mockito-core:2.+'

    // https://mvnrepository.com/artifact/org.apache.commons/commons-lang3
    implementation group: 'org.apache.commons', name: 'commons-lang3', version: '3.8.1'
}

repositories {
    mavenCentral()
    maven {
        url "https://hyperledger.jfrog.io/hyperledger/fabric-maven"
    }
    maven {
        url 'https://jitpack.io'
    }
}

application {
    mainClass = 'org.hyperledger.fabric.contract.ContractRouter'
}

sourceSets {
    main {
        java {
            exclude 'gui/**'
        }
    }
}

//checkstyle {
//    toolVersion '8.21'
//    configFile file("config/checkstyle/checkstyle.xml")
//}

//checkstyleMain {
//    source ='src'
//}

jacocoTestReport {
    dependsOn test
}

jacocoTestCoverageVerification {
    violationRules {
        rule {
            limit {
                minimum = 0.9
            }
        }
    }

    finalizedBy jacocoTestReport
}

test {
    useJUnitPlatform()
    testLogging {
        events "passed", "skipped", "failed"
    }
}

mainClassName = 'org.hyperledger.fabric.contract.ContractRouter'

shadowJar {
    baseName = 'chaincode'
    version = null
    classifier = null

    manifest {
        attributes 'Main-Class': 'org.hyperledger.fabric.contract.ContractRouter'
    }
}

check.dependsOn jacocoTestCoverageVerification
installDist.dependsOn check